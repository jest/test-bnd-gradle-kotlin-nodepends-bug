/*
 * Master Gradle build script
 *
 * Depends on bndPlugin property set by settings.gradle.
 * and bnd_* values from gradle.properties.
 */

import aQute.bnd.build.Workspace
import aQute.bnd.osgi.Constants

/* Add bnd gradle plugin as a script dependency */
buildscript {
  ext.kotlin_version = '1.0.5'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath bndPlugin
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

/* Initialize the bnd workspace */
Workspace.setDriver(Constants.BNDDRIVER_GRADLE)
Workspace.addGestalt(Constants.GESTALT_BATCH, null)
ext.bndWorkspace = new Workspace(rootDir, bnd_cnf).setOffline(gradle.startParameter.offline)

ext.cnf = rootProject.project(bnd_cnf)

/* Configure the subprojects */
subprojects {
  def bndProject = bndWorkspace.getProject(name)
  if (bndProject != null) {
    plugins.apply 'biz.aQute.bnd'
    plugins.withType(org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapper) {
      bndProject.getDependson()*.getName().each { dependency ->
        compileKotlin.dependsOn(":${dependency}:assemble")
      }
      repositories {
      	mavenCentral()
      }
      dependencies {
      	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
      }
    }
  }
}

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}
